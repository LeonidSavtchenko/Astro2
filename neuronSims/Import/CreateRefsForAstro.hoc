
// This one will be used in Astro to access the imported sections and extra space for dendrites (List of SectionRef)
objref dendrite_ref


// Create references to soma and dendrite sections imported from HOC file
// Also, allocate extra space for dendrites
// $s1 - Imported soma name
// $s2 - Imported dendrite name
// $3 - 0/1 flag indicating that we imported an old-style HOC file (designed to be input of Astro 1.0)
// Other input: NumberDendrites and OriginalDendrite (taken from top level)
// Output: soma_ref and dendrite_ref (initialized on top level)
proc createSomaAndDendriteReferencesForAstro() { local isOldStyleAstroHocFile, idx localobj nil
    strdef importedSomaName, importedDendriteName, command
    importedSomaName = $s1
    importedDendriteName = $s2
    isOldStyleAstroHocFile = $3
    
    // Initialize soma_ref
    createSomaReferences(importedSomaName)
    
    // Initialize dendrite_ref
    dendrite_ref = new List()
    forsec importedDendriteName {
        sprint(command, "%s _secRef = new SectionRef()", secname())
        execute(command)
        dendrite_ref.append(_secRef)
    }
    if (!isOldStyleAstroHocFile) {
        for (idx = 0; idx < NumberDendrites - OriginalDendrite; idx += 1) {
            _extraDendrite[idx] _secRef = new SectionRef()
            dendrite_ref.append(_secRef)
        }
    }
    
    // Some cleanup on top level
    _secRef = nil
}
